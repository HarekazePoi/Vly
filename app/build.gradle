apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-kapt"
apply plugin: 'com.google.ar.sceneform.plugin'

if(file('sampledata/models/internal.fbx').exists()) {
    sceneform.asset('sampledata/models/internal.fbx', // 'Source Asset Path' specified during import.
            'default',                            // 'Material Path' specified during import.
            'sampledata/models/internal.sfa',         // '.sfa Output Path' specified during import.
            'src/main/res/raw/internal')
}
android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "date.liyin.vly"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        ndkVersion "21.3.6528147"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            ndk {
                abiFilters = ["armeabi-v7a", "arm64-v8a", "x86"]
            }
        }
    }
    signingConfigs {
        release {
            storeFile file("../vlyautobuild.jks")
            storePassword 'vlyautobuild'
            keyAlias 'vlyautobuild'
            keyPassword 'vlyautobuild'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

// To inline the bytecode built with JVM target 1.8 into
// bytecode that is being built with JVM target 1.6. (e.g. navArgs)


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            version "3.10.2"
        }
    }
}

preBuild.doFirst {
    file("src/main/java/date/liyin/vly/INTERNAL.kt").write("""package date.liyin.vly
/*
This file is Generated by Gradle.
*/
class INTERNAL {
    companion object {
        fun getInternalModel() : Int? = ${ if(file("sampledata/models/internal.fbx").exists()) "R.raw.internal" else "null" }
    }
}
        """)
    println("INTERNAL settings generated.")
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'
    implementation "androidx.room:room-runtime:2.2.5"
    implementation 'androidx.room:room-ktx:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'
    implementation 'com.google.ar:core:1.15.0'
    implementation 'com.google.ar.sceneform.ux:sceneform-ux:1.15.0'
    implementation 'com.google.ar.sceneform:core:1.15.0'
    implementation 'com.google.ar.sceneform:animation:1.15.0'
    implementation 'com.google.ar.sceneform:assets:1.15.0'
    implementation 'com.chibatching.kotpref:kotpref:2.10.0'
    implementation 'com.chibatching.kotpref:initializer:2.10.0'
    implementation 'com.github.princekin-f:EasyFloat:1.3.0'
    implementation 'org.flazr:library:1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'party.liyin:skyrecycleradapter:2.4@aar'
    compileOnly 'party.liyin:aralib:2.4'
    kapt 'party.liyin:aralib-processor:2.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
